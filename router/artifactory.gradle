apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'

ext {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())

    ArtifactID = 'mrouter-router'
    Packaging = 'aar'
    artifactVersion  = '2.0.002-SNAPSHOT'
// def artifactVersion = '1.0.0-SNAPSHOT'//发布的快照版本
    GroupID = 'com.wuba.mobile.lib'
    ContextUrl = properties.getProperty("artifactory_contextUrl")
    ReleaseRepoKey = properties.getProperty("artifactory_release_repoKey")
    SnapshotRepoKey = properties.getProperty("artifactory_snapshot_repoKey")
    UserName = properties.getProperty("artifactory_user")
    Password = properties.getProperty("artifactory_password")
}

publishing {
    publications {
        aar(MavenPublication) {
            groupId GroupID
            version = artifactVersion
            artifactId ArtifactID
            // Tell maven to prepare the generated "* .aar" file for publishing
            artifact("$buildDir/outputs/aar/${project.name}-release.aar")

            pom.withXml {
                def root = asNode()
                def license = root.appendNode('licenses').appendNode('license')
                license.appendNode('name', 'The Apache Software License, Version 2.0')
                license.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
                license.appendNode('distribution', 'repo')

                def dependenciesNode = asNode().appendNode('dependencies')
                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.api.allDependencies.each {
                    if(it.group==null || it.name==null || it.version==null) return
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
}

artifactory {
    contextUrl = ContextUrl//"${artifactory_contextUrl}"
    publish {
        repository {
            // The Artifactory repository key to publish to
            repoKey = artifactVersion.endsWith('SNAPSHOT') ? SnapshotRepoKey : ReleaseRepoKey
            username = UserName//"${artifactory_user}" // The publisher user name
            password = Password//"${artifactory_password}" // The publisher password
            maven = true
        }
        defaults {
            publishArtifacts = true
            publications(Packaging)
            publishPom = true //Publish generated POM files to Artifactory (true by default)
            publishIvy = false //Publish generated Ivy descriptor files to Artifactory (true by default)
        }
    }
    resolve {
        repository {
            repoKey = 'jcenter'
            username = UserName // The resolver user name
            password = Password // The resolver password
        }
    }
}

// update lib command.
// windows: gradlew assembleRelease artifactoryPublish
// mac : permission denied ->
// mac : chmod 777 gradlew
// ./gradlew assembleRelease generatePomFileForAarPublication  artifactoryPublish